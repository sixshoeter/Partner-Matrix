database partners_na
screen size 40 by 102
{
--- PARTNER INFORMATION --- (USE UPDATE TO EDIT PARTNER DATA) -----------------------
CEID:[p01       ] NAME:[p02                                                         ]
 VAD:[a1        ] Headquarters:[b1] Segment:[c2] Reseller Code:[z] Lead:[y ] Tier:[w]
--- ASSIGNMENTS -------------------------------------------------------------
IBM Brand Rep Name: [p03                 ]                           [ts1                ]
   IM TSS Rep Name: [p04im               ]  Note: [note1                                   ]
  ECM TSS Rep Name: [p04em               ]        [note1                                   ]
   BA TSS Rep Name: [p04ba               ]        [note1                                   ]
      TSS Rep Name: [p04s1               ] TSS Rep Name: [p04s2               ]
                  URL: [p05                                               ]
Partner World Profile: [p06                                               ]
                       [p06                                               ]
Partner Contact: [p08                           ]  Title: [p08a                                    ]
          Phone: [p09                           ]  Email: [p10                                     ]
Other Focus Areas: 
1:[p11                           ]2:[p12                           ]3:[p13                           ]
--- DESCRIPTION -------------------------------------------------------------
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
}

screen
{
--- PARTNER INFORMATION -------------------------------------------------------------
	CEID: [p01       ] NAME: [p02                                                         ]
REGION                          ENTERPRISE UNITS
US West                     [a] Pacific Northwest
US West                     [b] North California
US West                     [c] Southwest & Rockies

US Midwest                  [e] Northern Stars
US Midwest                  [g] Plains
US Midwest                  [h] Heartland
US Midwest                  [i] Texas/New Mexico/Gulf

US Great Lakes/South        [q] Mid-America
US Great Lakes/South        [r] Ohio Valley
US Great Lakes/South        [s] Southeast
US Great Lakes/South        [t] Sunshine Belt

US North                    [m] Northeast
US North                    [l] Garden State
US North                    [d] NY Metro

US East                     [n] Delaware Valley
US East                     [o] Potomac Valley
US East                     [p] Carolinas

Canada                      [u] Canada
Federal                     [x] Federal
}

screen
{
--- PARTNER INFORMATION -------------------------------------------------------------
CEID: [p01       ] NAME: [p02                                                         ]

-------------------------------------------------------------
                               INDUSTRY
-------------------------------------------------------------

                    [m1] Energy & Utility
                    [m2] Telecommunication
                    [m3] Media & Entertainment
                    [m4] Retail
                    [m5] Wholesale Distribution
                    [m6] Transportation
                    [m7] Banking & Financial Market
                    [m8] Insurance
                    [m9] Manufacturing
                    [mm] Healthcare & Lifescience
                    [mn] Government
                    [mo] CROSS-INDUSTRY
}

screen
{
--- PARTNER INFORMATION -------------------------------------------------------------
CEID: [p01       ] NAME: [p02                                                         ]
-------------------------------------------------------------
                         BA TECHNICAL SKILLS (USE UPDATE TO EDIT)  [ts2                ]
-------------------------------------------------------------
   *** BUSINESS INTELLIGENCE ***               *** PREDICTIVE ANALYTICS ***
( 1) [u0] Cognos Enterprise                  ( 5) [e0] SPSS Data Collection
( 2) [v0] Cognos Express                     ( 6) [f0] SPSS Statistics
( 3) [w0] Cognos Insight                     ( 7) [g0] SPSS Modeler
( 4) [x0] Watson Analytics                   ( 8) [h0] SPSS Modeler Gold
   *** PERFORMANCE MANAGEMENT ***              *** SOLUTIONS ***
( 9) [i0] TM1                                (17) [q0] Financial and Operational Perf Mgmt
(10) [j0] Cognos Disclosure Management       (18) [r0] Narrative Reporting and Disclosure Mgmt
(12) [k0] Controller                         (19) [s0] Sales Performance Management
(12) [l0] Incentive Compensation Management  (20) [t0] Predictive Customer Intelligence
(13) [m0] Territory Management               (21) [t1] Predictive Maintenance and Quality
(14) [n0] Concert                            (22) [t2] Capacity Management Analytics
   *** RISK ANALYTICS ***                    (23) [t3] Forward looking Business Intelligence
(15) [o0] OpenPages                          (24) [t4] Predictive Analytics Enterprise solutions
(16) [p0] Algorithmics                       (25) [t5] Financial Risk Mgmt Banking and Fin Markets 
                                             (26) [t6] Financial Risk Management Insurers
                                             (27) [t7] Risk Information Governance
                                             (28) [t8] Governance, Risk and Compliance
   *** DECISION OPTIMIZATION
(29) [h1] Decision Optimization Center
(30) [h2] CPLEX
-------------------------------------------------------------------------------
    3-TOP Skilled Partner, with multiple successful engagements
    2-Skilled Partner, with successful engagements
    1-Learning partner
}
screen
{
--- PARTNER INFORMATION -------------------------------------------------------------
CEID: [p01       ] NAME: [p02                                                         ]
------------------------------------------------------------------
                SUCCESS STORIES & REFERENCES (USE ADD to ADD ROWS)
------------------------------------------------------------------

 Title 
[s01                                                                                       ]
[s01                               ]

 Link
[s02                                                                                 ]
[s02                                                                                 ]
[s02                                                                                 ]


}
screen
{
--- PARTNER INFORMATION -------------------------------------------------------------
CEID: [p01       ] NAME: [p02                                                         ]
--------------------------------------------------------------------
                         ADDITIONAL CONTACTS (USE ADD TO ADD ROWS)
--------------------------------------------------------------------

Partner Contact: [c08                           ]  Title: [c08a                                    ]
          Phone: [c09                           ]  Email: [c10                                     ]

          Region Focus: [c004                ]
             EUS Focus: [c005                     ]

}
end

tables
   coverage_au
   skills_ba
   success_au
   contacts_au
   reps
   reps2
   reps3
   reps4
   reps5
   reps6
   vads
   states
   eus
   eus2

attributes
p01 = coverage_au.ceid, downshift, queryclear, noupdate, required, default="";
    = skills_ba.ceid, downshift, queryclear, noentry, noupdate, color=reverse;
    = success_au.ceid, downshift, queryclear, noentry, noupdate, color=reverse;
    = contacts_au.c_ceid, downshift, queryclear, noentry, noupdate, color=reverse;
p02 = coverage_au.part_name, queryclear, required, color=reverse,
      comments="Partner Name";
a1   = coverage_au.vad=*vads.v_id, queryclear, default="",
      comments="VAD";
b1   = coverage_au.hq=*states.s_id, upshift, autonext, queryclear,
      comments="HQ: State where partner is headquartered";
c2   = coverage_au.part_type, upshift, include=("B","G","O","S"," "), autonext, required,
      comments="Segment: (B)lue Core, (G)rowth, (O)pportunistic, (S)trategic";
z   = coverage_au.reseller, upshift, include=("R","E","N","H","V"), default="R", autonext,
      comments="Reseller Code: (R)eseller, (E)merging, (H)igh Value, (V)ad, (N)ot Reselling";
note1   = coverage_au.note, wordwrap compress,
      comments="Internal (to Coverage Team) Notes regarding this Partner";
y   = coverage_au.lead, upshift, include=("BA","IM","EM","IO", " ",""), autonext, 
      comments="Technical Lead: BA, IM, EM ";
w   = coverage_au.tier, autonext, include=(0,1,2,3),
      comments="Partner is part of this Tier 0-3";
p03 = coverage_au.ssr=reps.r_name, queryclear, default="",
      comments="IBM Channels Brand Rep";
p04im = coverage_au.ctp1=reps2.r_name, queryclear, default="",
      comments="IBM Channels IM TSS Rep (use a single space to indicate empty)";
p04em = coverage_au.ctp2=reps3.r_name, queryclear, default="",
      comments="IBM Channels ECM TSS Rep (use a single space to indicate empty)";
p04ba = coverage_au.ctp3=reps4.r_name, queryclear, default="",
      comments="IBM Channels BA TSS Rep (use a single space to indicate empty)";
p04s1 = coverage_au.ctp4=reps5.r_name, queryclear, default="",
      comments="IBM Channels TSS Rep (use a single space to indicate empty)";
p04s2 = coverage_au.ctp5=reps6.r_name, queryclear, default="",
      comments="IBM Channels TSS Rep (use a single space to indicate empty)";
p05 = coverage_au.url,
      comments="Partners Corporate URL";
p06 = coverage_au.pwp, wordwrap compress,
      comments="Partners Partners World Profile URL";
p08 = coverage_au.p_name,
      comments="Partner contact name for passing leads";
p08a = coverage_au.p_title,
      comments="Partner contact title";
p09 = coverage_au.p_phone,
      comments="Partner contact phone number (with area code): ###.###.#### x######";
p10 = coverage_au.p_email,
      comments="Partner contact corporate email address";
p11 = coverage_au.focus1, 
      comments="Other focus areas of partner (e.g., Mobility, etc)";
p12 = coverage_au.focus2, 
      comments="Other focus areas of partner (e.g., Mobility, etc)";
p13 = coverage_au.focus3, 
      comments="Other focus areas of partner (e.g., Mobility, etc)";
p07 = coverage_au.desc, wordwrap compress,
      comments="Description about the partner - use <p> to separate paragraphs";


a = coverage_au.eu_pacificnw, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
b = coverage_au.eu_northca, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
c = coverage_au.eu_southwest, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
e = coverage_au.eu_northern, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
g = coverage_au.eu_plains, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
h = coverage_au.eu_heartland, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
i = coverage_au.eu_texas, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
q = coverage_au.eu_midamerica, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
r = coverage_au.eu_ohio, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
s = coverage_au.eu_southeast, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
t = coverage_au.eu_sunshine, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
m = coverage_au.eu_northeast, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
l = coverage_au.eu_garden, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
d = coverage_au.eu_ny, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
n = coverage_au.eu_delaware, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
o = coverage_au.eu_potomac, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
p = coverage_au.eu_carolinas, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
u = coverage_au.eu_ca, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
x = coverage_au.eu_fed, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";

m1 = coverage_au.ind_eu, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
m2 = coverage_au.ind_tel, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
m3 = coverage_au.ind_me, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
m4 = coverage_au.ind_ret, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
m5 = coverage_au.ind_wd, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
m6 = coverage_au.ind_trans, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
m7 = coverage_au.ind_bank, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
m8 = coverage_au.ind_ins, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
m9 = coverage_au.ind_mfg, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
mm = coverage_au.ind_hl, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
mn = coverage_au.ind_gov, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
mo = coverage_au.ind_cross, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
ts1 = coverage_au.updtime;

u0 = skills_ba.skill_01, include=(null, 1, 2, 3), autonext;
v0 = skills_ba.skill_02, include=(null, 1, 2, 3), autonext;
w0 = skills_ba.skill_03, include=(null, 1, 2, 3), autonext;
x0 = skills_ba.skill_04, include=(null, 1, 2, 3), autonext;
e0 = skills_ba.skill_05, include=(null, 1, 2, 3), autonext;
f0 = skills_ba.skill_06, include=(null, 1, 2, 3), autonext;
g0 = skills_ba.skill_07, include=(null, 1, 2, 3), autonext;
h0 = skills_ba.skill_08, include=(null, 1, 2, 3), autonext;
i0 = skills_ba.skill_09, include=(null, 1, 2, 3), autonext;
j0 = skills_ba.skill_10, include=(null, 1, 2, 3), autonext;
k0 = skills_ba.skill_11, include=(null, 1, 2, 3), autonext;
l0 = skills_ba.skill_12, include=(null, 1, 2, 3), autonext;
m0 = skills_ba.skill_13, include=(null, 1, 2, 3), autonext;
n0 = skills_ba.skill_14, include=(null, 1, 2, 3), autonext;
o0 = skills_ba.skill_15, include=(null, 1, 2, 3), autonext;
p0 = skills_ba.skill_16, include=(null, 1, 2, 3), autonext;
q0 = skills_ba.skill_17, include=(null, 1, 2, 3), autonext;
r0 = skills_ba.skill_18, include=(null, 1, 2, 3), autonext;
s0 = skills_ba.skill_19, include=(null, 1, 2, 3), autonext;
t0 = skills_ba.skill_20, include=(null, 1, 2, 3), autonext;
t1 = skills_ba.skill_21, include=(null, 1, 2, 3), autonext;
t2 = skills_ba.skill_22, include=(null, 1, 2, 3), autonext;
t3 = skills_ba.skill_23, include=(null, 1, 2, 3), autonext;
t4 = skills_ba.skill_24, include=(null, 1, 2, 3), autonext;
t5 = skills_ba.skill_25, include=(null, 1, 2, 3), autonext;
t6 = skills_ba.skill_26, include=(null, 1, 2, 3), autonext;
t7 = skills_ba.skill_27, include=(null, 1, 2, 3), autonext;
t8 = skills_ba.skill_28, include=(null, 1, 2, 3), autonext;
h1 = skills_ba.skill_29, include=(null, 1, 2, 3), autonext;
h2 = skills_ba.skill_30, include=(null, 1, 2, 3), autonext;

ts2= skills_ba.updtime; 

s01 = success_au.s_title, wordwrap compress,
      comments="Short descriptive title for Success Story";
s02 = success_au.s_link, wordwrap compress,
      comments="URL that points to the Success Story";


c08 = contacts_au.c_name,
      comments="Partner contact name for passing leads";
c08a = contacts_au.c_title,
      comments="Partner contact title";
c09 = contacts_au.c_phone,
      comments="Partner contact phone number (with area code)";
c10 = contacts_au.c_email,
      comments="Partner contact corporate email address";
c004 = contacts_au.e_region=*eus.e_region, default="<none>",
      comments="Region Focus for Contact";
c005 = contacts_au.e_name=*eus2.e_name, default="<none>",
      comments="EU Focus for Contact";


instructions
   delimiters "  ";


   coverage_au master of skills_ba
   skills_ba master of success_au
   success_au master of contacts_au


   after editadd editupdate of coverage_au
      let ts1 = current
      if( p03 is null and p04im is null and p04em is null and p04ba is null and p04s1 is null and p04s2 is null and z="R" )then
         let z = "N"
      if( p03 = " " and p04im = " " and p04em = " " and p04ba = " " and p04s1 = " " and p04s2 = " " and z="R" )then
         let z = "N"

   before editadd editupdate of coverage_au.updtime
      nextfield = p02

   after editadd editupdate of skills_ba
      let ts2 = current

   before editadd editupdate of skills_ba.updtime
      nextfield = u0


      end
