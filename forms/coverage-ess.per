database partners_na
screen size 40 by 102
{
--- PARTNER INFORMATION -------------------------------------------------------------
CEID:[p01       ] NAME:[p02                                                         ]
 VAD:[a1        ] Headquarters:[b1] Segment:[c2] Reseller Code:[z] Lead:[y ] Tier:[w]
--- ASSIGNMENTS -------------------------------------------------------------
IBM Brand Rep Name: [p03                 ]                           [ts1                ]
   IM TSS Rep Name: [p04im               ]  Notes: [note1                                   ]
  ECM TSS Rep Name: [p04em               ]         [note1                                   ]
   BA TSS Rep Name: [p04ba               ]         [note1                                   ]

                  URL: [p05                                               ]
Partner World Profile: [p06                                               ]
                       [p06                                               ]
Partner Contact: [p08                           ]  Title: [p08a                                    ]
          Phone: [p09                           ]  Email: [p10                                     ]
Hunter:[j]  Other Focus Areas: 
1:[p11                           ]2:[p12                           ]3:[p13                           ]
--- DESCRIPTION -------------------------------------------------------------
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
[p07                                                                                      ]
}

screen
{
--- PARTNER INFORMATION -------------------------------------------------------------
	CEID: [p01       ] NAME: [p02                                                         ]
REGION                          ENTERPRISE UNITS
US West                     [a] Pacific Northwest
US West                     [b] North California
US West                     [c] Southwest & Rockies

US Midwest                  [e] Northern Stars
US Midwest                  [g] Plains
US Midwest                  [h] Heartland
US Midwest                  [i] Texas/New Mexico/Gulf

US Great Lakes/South        [q] Mid-America
US Great Lakes/South        [r] Ohio Valley
US Great Lakes/South        [s] Southeast
US Great Lakes/South        [t] Sunshine Belt

US North                    [m] Northeast
US North                    [l] Garden State
US North                    [d] NY Metro

US East                     [n] Delaware Valley
US East                     [o] Potomac Valley
US East                     [p] Carolinas

Canada                      [u] Canada
Federal                     [x] Federal
}

screen
{
--- PARTNER INFORMATION -------------------------------------------------------------
CEID: [p01       ] NAME: [p02                                                         ]

-------------------------------------------------------------
                               INDUSTRY
-------------------------------------------------------------

                    [m1] Energy & Utility
                    [m2] Telecommunication
                    [m3] Media & Entertainment
                    [m4] Retail
                    [m5] Wholesale Distribution
                    [m6] Transportation
                    [m7] Banking & Financial Market
                    [m8] Insurance
                    [m9] Manufacturing
                    [mm] Healthcare & Lifescience
                    [mn] Government
                    [mo] CROSS-INDUSTRY
}

screen
{
--- PARTNER INFORMATION -------------------------------------------------------------
CEID: [p01       ] NAME: [p02                                                         ]
-------------------------------------------------------------
                         ESS TECHNICAL SKILLS            [ts2                ]
-------------------------------------------------------------

MESSAGING & COLLABORATION
( 1)[u0] IBM Domino
( 2)[r0] Notes
( 3)[f0] Protector

SOCIAL SOFTWARE
( 4)[i0] Connections
( 5)[j0] IBM Docs
( 6)[g0] Sametime

SaaS Entry
( 7)[h0] Connections Cloud
( 8)[l0] Verse
( 9)[e0] SmartCloud Notes Migrations
(10)[n0] Traveler
-------------------------------------------------------------------------------
    3-TOP Skilled Partner, with multiple successful engagements
    2-Skilled Partner, with successful engagements
    1-Learning partner

}
screen
{
--- PARTNER INFORMATION -------------------------------------------------------------
CEID: [p01       ] NAME: [p02                                                         ]
------------------------------------------------------------------
                          SUCCESS STORIES & REFERENCES (ADD ROWS)
------------------------------------------------------------------

 Title 
[s01                                                                                       ]
[s01                               ]

 Link
[s02                                                                                 ]
[s02                                                                                 ]
[s02                                                                                 ]


}
screen
{
--- PARTNER INFORMATION -------------------------------------------------------------
CEID: [p01       ] NAME: [p02                                                         ]
-------------------------------------------------------------
                         ADDITIONAL CONTACTS
-------------------------------------------------------------

Partner Contact: [c08                           ]  Title: [c08a                                    ]
          Phone: [c09                           ]  Email: [c10                                     ]

          Region Focus: [c004                ]
             EUS Focus: [c005                     ]

}
end

tables
   coverage_es
   skills_es
   success_es
   contacts_es
   reps
   reps2
   reps3
   reps4
   vads
   states
   eus
   eus2

attributes
p01 = coverage_es.ceid, downshift, queryclear, noupdate, required, default="";
    = skills_es.ceid, downshift, queryclear, noentry, noupdate, color=reverse;
    = success_es.ceid, downshift, queryclear, noentry, noupdate, color=reverse;
    = contacts_es.c_ceid, downshift, queryclear, noentry, noupdate, color=reverse;
p02 = coverage_es.part_name, queryclear, required, color=reverse,
      comments="Partner Name";
a1   = coverage_es.vad=*vads.v_id, queryclear, default="",
      comments="VAD";
b1   = coverage_es.hq=*states.s_id, upshift, autonext, queryclear,
      comments="HQ: State where partner is headquartered";
c2   = coverage_es.part_type, upshift, include=("B","G","O","S"," "), autonext, required,
      comments="Segment: (B)lue Core, (G)rowth, (O)pportunistic, (S)trategic";
z   = coverage_es.reseller, upshift, include=("R","E","N","H","V"), default="R", autonext,
      comments="Reseller Code: (R)eseller, (E)merging, (H)igh Value, (V)ad, (N)ot Reselling";
note1   = coverage_es.note, wordwrap compress,
      comments="Internal (to Coverage Team) Notes regarding this Partner";
y   = coverage_es.lead, upshift, include=("BA","IM","EM","IO"," ",""), autonext, 
      comments="Technical Lead: BA, IM, EM ";
w   = coverage_es.tier, autonext, include=(0,1,2,3),
      comments="Partner is part of this Tier 0-3";
p03 = coverage_es.ssr=*reps.r_name, queryclear, default="",
      comments="IBM Channels Brand Rep";
p04im = coverage_es.ctp1=*reps2.r_name, queryclear, default="",
      comments="IBM Channels IM TSS Rep (use a single space to indicate empty)";
p04em = coverage_es.ctp2=*reps3.r_name, queryclear, default="",
      comments="IBM Channels ECM TSS Rep (use a single space to indicate empty)";
p04ba = coverage_es.ctp3=*reps4.r_name, queryclear, default="",
      comments="IBM Channels BA TSS Rep (use a single space to indicate empty)";
p05 = coverage_es.url,
      comments="Partners Corporate URL";
p06 = coverage_es.pwp, wordwrap compress,
      comments="Partners Partners World Profile URL";
p08 = coverage_es.p_name,
      comments="Partner contact name for passing leads";
p08a = coverage_es.p_title,
      comments="Partner contact title";
p09 = coverage_es.p_phone,
      comments="Partner contact phone number (with area code)";
p10 = coverage_es.p_email,
      comments="Partner contact corporate email address";
j   = coverage_es.hunter, upshift, autonext,
      comments="Partner is a Hunter - going after white space";
p11 = coverage_es.focus1, 
      comments="Other focus areas of partner (e.g., Mobility, etc)";
p12 = coverage_es.focus2, 
      comments="Other focus areas of partner (e.g., Mobility, etc)";
p13 = coverage_es.focus3, 
      comments="Other focus areas of partner (e.g., Mobility, etc)";
p07 = coverage_es.desc, wordwrap compress,
      comments="Description about the partner - use <p> to separate paragraphs";


a = coverage_es.eu_pacificnw, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
b = coverage_es.eu_northca, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
c = coverage_es.eu_southwest, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
e = coverage_es.eu_northern, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
g = coverage_es.eu_plains, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
h = coverage_es.eu_heartland, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
i = coverage_es.eu_texas, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
q = coverage_es.eu_midamerica, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
r = coverage_es.eu_ohio, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
s = coverage_es.eu_southeast, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
t = coverage_es.eu_sunshine, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
m = coverage_es.eu_northeast, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
l = coverage_es.eu_garden, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
d = coverage_es.eu_ny, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
n = coverage_es.eu_delaware, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
o = coverage_es.eu_potomac, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
p = coverage_es.eu_carolinas, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
u = coverage_es.eu_ca, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";
x = coverage_es.eu_fed, autonext, upshift, include=("Y","H"," ",""),
    comments="If selling in this Enterprise Unit, use 'Y' - if HQ there, use 'H'";

m1 = coverage_es.ind_eu, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
m2 = coverage_es.ind_tel, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
m3 = coverage_es.ind_me, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
m4 = coverage_es.ind_ret, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
m5 = coverage_es.ind_wd, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
m6 = coverage_es.ind_trans, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
m7 = coverage_es.ind_bank, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
m8 = coverage_es.ind_ins, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
m9 = coverage_es.ind_mfg, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
mm = coverage_es.ind_hl, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
mn = coverage_es.ind_gov, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
mo = coverage_es.ind_cross, autonext, upshift, include=("Y"," ",""),
     comments="If this is an Industry focus, use 'Y'";
ts1 = coverage_es.updtime;

u0 = skills_es.skill_01, include=(null, 1, 2, 3), autonext;
r0 = skills_es.skill_18, include=(null, 1, 2, 3), autonext;
f0 = skills_es.skill_06, include=(null, 1, 2, 3), autonext;
i0 = skills_es.skill_04, include=(null, 1, 2, 3), autonext;
j0 = skills_es.skill_03, include=(null, 1, 2, 3), autonext;
g0 = skills_es.skill_09, include=(null, 1, 2, 3), autonext;
h0 = skills_es.skill_10, include=(null, 1, 2, 3), autonext;
l0 = skills_es.skill_07, include=(null, 1, 2, 3), autonext;
e0 = skills_es.skill_08, include=(null, 1, 2, 3), autonext;
n0 = skills_es.skill_12, include=(null, 1, 2, 3), autonext;
ts2 = skills_es.updtime;


s01 = success_es.s_title, wordwrap compress,
      comments="Short descriptive title for Success Story";
s02 = success_es.s_link, wordwrap compress,
      comments="URL that points to the Success Story";


c08 = contacts_es.c_name,
      comments="Partner contact name for passing leads";
c08a = contacts_es.c_title,
      comments="Partner contact title";
c09 = contacts_es.c_phone,
      comments="Partner contact phone number (with area code)";
c10 = contacts_es.c_email,
      comments="Partner contact corporate email address";
c004 = contacts_es.e_region=*eus.e_region, default="<none>",
      comments="Region Focus for Contact";
c005 = contacts_es.e_name=*eus2.e_name, default="<none>",
      comments="EU Focus for Contact";


instructions
   delimiters "  ";


   coverage_es master of skills_es
   skills_es master of success_es
   success_es master of contacts_es


   after editadd editupdate of coverage_es
      let ts1 = current
      if( p03 is null and p04im is null and p04em is null and p04ba is null and z="R" )then
         let z = "N"
      if( p03 = " " and p04im = " " and p04em = " " and p04ba = " " and z="R" )then
         let z = "N"

   before editadd editupdate of coverage_es.updtime
      nextfield = p02

   after editadd editupdate of skills_es
      let ts2 = current

   before editadd editupdate of skills_es.updtime
      nextfield = u0


      end
